/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, S√©bastien Gou√´zel, Jean Lo, Yury Kudryashov
-/
import topology.algebra.ring
import topology.uniform_space.uniform_embedding
import algebra.algebra.basic
import linear_algebra.projection

/-!
# Theory of topological modules and continuous linear maps.

We define classes `topological_semimodule`, `topological_module` and `topological_vector_spaces`,
as extensions of the corresponding algebraic classes where the algebraic operations are continuous.

We also define continuous linear maps, as linear maps between topological modules which are
continuous. The set of continuous linear maps between the topological `R`-modules `M` and `M‚ÇÇ` is
denoted by `M ‚ÜíL[R] M‚ÇÇ`.

Continuous linear equivalences are denoted by `M ‚âÉL[R] M‚ÇÇ`.

## Implementation notes

Topological vector spaces are defined as an `abbreviation` for topological modules,
if the base ring is a field. This has as advantage that topological vector spaces are completely
transparent for type class inference, which means that all instances for topological modules
are immediately picked up for vector spaces as well.
A cosmetic disadvantage is that one can not extend topological vector spaces.
The solution is to extend `topological_module` instead.
-/

open filter
open_locale topological_space big_operators

universes u v w u'

/-- A topological semimodule, over a semiring which is also a topological space, is a
semimodule in which scalar multiplication is continuous. In applications, R will be a topological
semiring and M a topological additive semigroup, but this is not needed for the definition -/
class topological_semimodule (R : Type u) (M : Type v)
  [semiring R] [topological_space R]
  [topological_space M] [add_comm_monoid M]
  [semimodule R M] : Prop :=
(continuous_smul : continuous (Œªp : R √ó M, p.1 ‚Ä¢ p.2))

section

variables {R : Type u} {M : Type v}
[semiring R] [topological_space R]
[topological_space M] [add_comm_monoid M]
[semimodule R M] [topological_semimodule R M]

lemma continuous_smul : continuous (Œªp:R√óM, p.1 ‚Ä¢ p.2) :=
topological_semimodule.continuous_smul

@[continuity]
lemma continuous.smul {Œ± : Type*} [topological_space Œ±] {f : Œ± ‚Üí R} {g : Œ± ‚Üí M}
  (hf : continuous f) (hg : continuous g) : continuous (Œªp, f p ‚Ä¢ g p) :=
continuous_smul.comp (hf.prod_mk hg)

lemma tendsto_smul {c : R} {x : M} : tendsto (Œªp:R√óM, p.fst ‚Ä¢ p.snd) (ùìù (c, x)) (ùìù (c ‚Ä¢ x)) :=
continuous_smul.tendsto _

lemma filter.tendsto.smul {Œ± : Type*} {l : filter Œ±} {f : Œ± ‚Üí R} {g : Œ± ‚Üí M} {c : R} {x : M}
  (hf : tendsto f l (ùìù c)) (hg : tendsto g l (ùìù x)) : tendsto (Œª a, f a ‚Ä¢ g a) l (ùìù (c ‚Ä¢ x)) :=
tendsto_smul.comp (hf.prod_mk_nhds hg)

end

instance topological_semiring.to_semimodule {R : Type*} [topological_space R]
  [semiring R] [topological_semiring R] :
  topological_semimodule R R :=
{ continuous_smul := continuous_mul }

/-- A topological module, over a ring which is also a topological space, is a module in which
scalar multiplication is continuous. In applications, `R` will be a topological ring and `M` a
topological additive group, but this is not needed for the definition -/
abbreviation topological_module (R : Type u) (M : Type v)
  [ring R] [topological_space R]
  [topological_space M] [add_comm_group M] [module R M] :=
topological_semimodule R M

/-- A topological vector space is a topological module over a field. -/
abbreviation topological_vector_space (R : Type u) (M : Type v)
  [field R] [topological_space R]
  [topological_space M] [add_comm_group M] [module R M] :=
topological_module R M

section

variables {R : Type*} {M : Type*}
[ring R] [topological_space R]
[topological_space M] [add_comm_group M]
[module R M] [topological_module R M]

/-- Scalar multiplication by a unit is a homeomorphism from a
topological module onto itself. -/
protected def homeomorph.smul_of_unit (a : units R) : M ‚âÉ‚Çú M :=
{ to_fun    := Œª x, (a : R) ‚Ä¢ x,
  inv_fun   := Œª x, ((a‚Åª¬π : units R) : R) ‚Ä¢ x,
  right_inv := Œª x, calc (a : R) ‚Ä¢ ((a‚Åª¬π : units R) : R) ‚Ä¢ x = x :
                 by rw [smul_smul, units.mul_inv, one_smul],
  left_inv  := Œª x, calc ((a‚Åª¬π : units R) : R) ‚Ä¢ (a : R) ‚Ä¢ x = x :
                 by rw [smul_smul, units.inv_mul, one_smul],
  continuous_to_fun  := continuous_const.smul continuous_id,
  continuous_inv_fun := continuous_const.smul continuous_id }

lemma is_open_map_smul_of_unit (a : units R) : is_open_map (Œª (x : M), (a : R) ‚Ä¢ x) :=
(homeomorph.smul_of_unit a).is_open_map

lemma is_closed_map_smul_of_unit (a : units R) : is_closed_map (Œª (x : M), (a : R) ‚Ä¢ x) :=
(homeomorph.smul_of_unit a).is_closed_map

/-- If `M` is a topological module over `R` and `0` is a limit of invertible elements of `R`, then
`‚ä§` is the only submodule of `M` with a nonempty interior.
This is the case, e.g., if `R` is a nondiscrete normed field. -/
lemma submodule.eq_top_of_nonempty_interior' [has_continuous_add M]
  [ne_bot (ùìù[{x : R | is_unit x}] 0)]
  (s : submodule R M) (hs : (interior (s:set M)).nonempty) :
  s = ‚ä§ :=
begin
  rcases hs with ‚ü®y, hy‚ü©,
  refine (submodule.eq_top_iff'.2 $ Œª x, _),
  rw [mem_interior_iff_mem_nhds] at hy,
  have : tendsto (Œª c:R, y + c ‚Ä¢ x) (ùìù[{x : R | is_unit x}] 0) (ùìù (y + (0:R) ‚Ä¢ x)),
    from tendsto_const_nhds.add ((tendsto_nhds_within_of_tendsto_nhds tendsto_id).smul
      tendsto_const_nhds),
  rw [zero_smul, add_zero] at this,
  rcases nonempty_of_mem_sets (inter_mem_sets (mem_map.1 (this hy)) self_mem_nhds_within)
    with ‚ü®_, hu, u, rfl‚ü©,
  have hy' : y ‚àà ‚Üës := mem_of_nhds hy,
  exact (s.smul_mem_iff' _).1 ((s.add_mem_iff_right hy').1 hu)
end

end

section

variables {R : Type*} {M : Type*} {a : R}
[field R] [topological_space R]
[topological_space M] [add_comm_group M]
[vector_space R M] [topological_vector_space R M]


/-- Scalar multiplication by a non-zero field element is a
homeomorphism from a topological vector space onto itself. -/
protected def homeomorph.smul_of_ne_zero (ha : a ‚â† 0) : M ‚âÉ‚Çú M :=
{.. homeomorph.smul_of_unit (units.mk0 a ha)}

lemma is_open_map_smul_of_ne_zero (ha : a ‚â† 0) : is_open_map (Œª (x : M), a ‚Ä¢ x) :=
(homeomorph.smul_of_ne_zero ha).is_open_map

/-- `smul` is a closed map in the second argument.

The lemma that `smul` is a closed map in the first argument (for a normed space over a complete
normed field) is `is_closed_map_smul_left` in `analysis.normed_space.finite_dimension`. -/
lemma is_closed_map_smul_of_ne_zero (ha : a ‚â† 0) : is_closed_map (Œª (x : M), a ‚Ä¢ x) :=
(homeomorph.smul_of_ne_zero ha).is_closed_map

end

/-- Continuous linear maps between modules. We only put the type classes that are necessary for the
definition, although in applications `M` and `M‚ÇÇ` will be topological modules over the topological
ring `R`. -/
structure continuous_linear_map
  (R : Type*) [semiring R]
  (M : Type*) [topological_space M] [add_comm_monoid M]
  (M‚ÇÇ : Type*) [topological_space M‚ÇÇ] [add_comm_monoid M‚ÇÇ]
  [semimodule R M] [semimodule R M‚ÇÇ]
  extends linear_map R M M‚ÇÇ :=
(cont : continuous to_fun . tactic.interactive.continuity')

notation M ` ‚ÜíL[`:25 R `] ` M‚ÇÇ := continuous_linear_map R M M‚ÇÇ

/-- Continuous linear equivalences between modules. We only put the type classes that are necessary
for the definition, although in applications `M` and `M‚ÇÇ` will be topological modules over the
topological ring `R`. -/
@[nolint has_inhabited_instance]
structure continuous_linear_equiv
  (R : Type*) [semiring R]
  (M : Type*) [topological_space M] [add_comm_monoid M]
  (M‚ÇÇ : Type*) [topological_space M‚ÇÇ] [add_comm_monoid M‚ÇÇ]
  [semimodule R M] [semimodule R M‚ÇÇ]
  extends linear_equiv R M M‚ÇÇ :=
(continuous_to_fun  : continuous to_fun . tactic.interactive.continuity')
(continuous_inv_fun : continuous inv_fun . tactic.interactive.continuity')

notation M ` ‚âÉL[`:50 R `] ` M‚ÇÇ := continuous_linear_equiv R M M‚ÇÇ

namespace continuous_linear_map

section semiring
/- Properties that hold for non-necessarily commutative semirings. -/

variables
{R : Type*} [semiring R]
{M : Type*} [topological_space M] [add_comm_monoid M]
{M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_monoid M‚ÇÇ]
{M‚ÇÉ : Type*} [topological_space M‚ÇÉ] [add_comm_monoid M‚ÇÉ]
{M‚ÇÑ : Type*} [topological_space M‚ÇÑ] [add_comm_monoid M‚ÇÑ]
[semimodule R M] [semimodule R M‚ÇÇ] [semimodule R M‚ÇÉ] [semimodule R M‚ÇÑ]

/-- Coerce continuous linear maps to linear maps. -/
instance : has_coe (M ‚ÜíL[R] M‚ÇÇ) (M ‚Üí‚Çó[R] M‚ÇÇ) := ‚ü®to_linear_map‚ü©

/-- Coerce continuous linear maps to functions. -/
-- see Note [function coercion]
instance to_fun : has_coe_to_fun $ M ‚ÜíL[R] M‚ÇÇ := ‚ü®Œª _, M ‚Üí M‚ÇÇ, Œª f, f‚ü©

@[simp] lemma coe_mk (f : M ‚Üí‚Çó[R] M‚ÇÇ) (h) : (mk f h : M ‚Üí‚Çó[R] M‚ÇÇ) = f := rfl
@[simp] lemma coe_mk' (f : M ‚Üí‚Çó[R] M‚ÇÇ) (h) : (mk f h : M ‚Üí M‚ÇÇ) = f := rfl

@[continuity]
protected lemma continuous (f : M ‚ÜíL[R] M‚ÇÇ) : continuous f := f.2

theorem coe_injective : function.injective (coe : (M ‚ÜíL[R] M‚ÇÇ) ‚Üí (M ‚Üí‚Çó[R] M‚ÇÇ)) :=
by { intros f g H, cases f, cases g, congr' 1, exact H }

theorem coe_fn_injective : function.injective (Œª f : M ‚ÜíL[R] M‚ÇÇ, show M ‚Üí M‚ÇÇ, from f) :=
linear_map.coe_injective.comp coe_injective

@[ext] theorem ext {f g : M ‚ÜíL[R] M‚ÇÇ} (h : ‚àÄ x, f x = g x) : f = g :=
coe_fn_injective $ funext h

theorem ext_iff {f g : M ‚ÜíL[R] M‚ÇÇ} : f = g ‚Üî ‚àÄ x, f x = g x :=
‚ü®Œª h x, by rw h, by ext‚ü©

variables (c : R) (f g : M ‚ÜíL[R] M‚ÇÇ) (h : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (x y z : M)

-- make some straightforward lemmas available to `simp`.
@[simp] lemma map_zero : f (0 : M) = 0 := (to_linear_map _).map_zero
@[simp] lemma map_add  : f (x + y) = f x + f y := (to_linear_map _).map_add _ _
@[simp] lemma map_smul : f (c ‚Ä¢ x) = c ‚Ä¢ f x := (to_linear_map _).map_smul _ _

lemma map_sum {Œπ : Type*} (s : finset Œπ) (g : Œπ ‚Üí M) :
  f (‚àë i in s, g i) = ‚àë i in s, f (g i) := f.to_linear_map.map_sum

@[simp, norm_cast] lemma coe_coe : ((f : M ‚Üí‚Çó[R] M‚ÇÇ) : (M ‚Üí M‚ÇÇ)) = (f : M ‚Üí M‚ÇÇ) := rfl

/-- If two continuous linear maps are equal on a set `s`, then they are equal on the closure
of the `submodule.span` of this set. -/
lemma eq_on_closure_span [t2_space M‚ÇÇ] {s : set M} {f g : M ‚ÜíL[R] M‚ÇÇ} (h : set.eq_on f g s) :
  set.eq_on f g (closure (submodule.span R s : set M)) :=
(linear_map.eq_on_span' h).closure f.continuous g.continuous

/-- If the submodule generated by a set `s` is dense in the ambient semimodule, then two continuous
linear maps equal on `s` are equal. -/
lemma ext_on [t2_space M‚ÇÇ] {s : set M} (hs : dense (submodule.span R s : set M)) {f g : M ‚ÜíL[R] M‚ÇÇ}
  (h : set.eq_on f g s) :
  f = g :=
ext $ Œª x, eq_on_closure_span h (hs x)

/-- The continuous map that is constantly zero. -/
instance: has_zero (M ‚ÜíL[R] M‚ÇÇ) := ‚ü®‚ü®0, continuous_const‚ü©‚ü©
instance : inhabited (M ‚ÜíL[R] M‚ÇÇ) := ‚ü®0‚ü©

@[simp] lemma default_def : default (M ‚ÜíL[R] M‚ÇÇ) = 0 := rfl
@[simp] lemma zero_apply : (0 : M ‚ÜíL[R] M‚ÇÇ) x = 0 := rfl
@[simp, norm_cast] lemma coe_zero : ((0 : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí‚Çó[R] M‚ÇÇ) = 0 := rfl
/- no simp attribute on the next line as simp does not always simplify `0 x` to `0`
when `0` is the zero function, while it does for the zero continuous linear map,
and this is the most important property we care about. -/
@[norm_cast] lemma coe_zero' : ((0 : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) = 0 := rfl

instance unique_of_left [subsingleton M] : unique (M ‚ÜíL[R] M‚ÇÇ) :=
coe_injective.unique

instance unique_of_right [subsingleton M‚ÇÇ] : unique (M ‚ÜíL[R] M‚ÇÇ) :=
coe_injective.unique

section

variables (R M)

/-- the identity map as a continuous linear map. -/
def id : M ‚ÜíL[R] M :=
‚ü®linear_map.id, continuous_id‚ü©

end

instance : has_one (M ‚ÜíL[R] M) := ‚ü®id R M‚ü©

lemma id_apply : id R M x = x := rfl
@[simp, norm_cast] lemma coe_id : (id R M : M ‚Üí‚Çó[R] M) = linear_map.id := rfl
@[simp, norm_cast] lemma coe_id' : (id R M : M ‚Üí M) = _root_.id := rfl

@[simp] lemma one_apply : (1 : M ‚ÜíL[R] M) x = x := rfl

section add
variables [has_continuous_add M‚ÇÇ]

instance : has_add (M ‚ÜíL[R] M‚ÇÇ) :=
‚ü®Œª f g, ‚ü®f + g, f.2.add g.2‚ü©‚ü©

@[simp] lemma add_apply : (f + g) x = f x + g x := rfl
@[simp, norm_cast] lemma coe_add : (((f + g) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí‚Çó[R] M‚ÇÇ) = (f : M ‚Üí‚Çó[R] M‚ÇÇ) + g := rfl
@[norm_cast] lemma coe_add' : (((f + g) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) = (f : M ‚Üí M‚ÇÇ) + g := rfl

instance : add_comm_monoid (M ‚ÜíL[R] M‚ÇÇ) :=
by { refine {zero := 0, add := (+), ..}; intros; ext;
  apply_rules [zero_add, add_assoc, add_zero, add_left_neg, add_comm] }

lemma sum_apply {Œπ : Type*} (t : finset Œπ) (f : Œπ ‚Üí M ‚ÜíL[R] M‚ÇÇ) (b : M) :
  (‚àë d in t, f d) b = ‚àë d in t, f d b :=
begin
  haveI : is_add_monoid_hom (Œª (g : M ‚ÜíL[R] M‚ÇÇ), g b) :=
    { map_add := Œª f g, continuous_linear_map.add_apply f g b, map_zero := by simp },
  exact (finset.sum_hom t (Œª g : M ‚ÜíL[R] M‚ÇÇ, g b)).symm
end

end add

/-- Composition of bounded linear maps. -/
def comp (g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (f : M ‚ÜíL[R] M‚ÇÇ) : M ‚ÜíL[R] M‚ÇÉ :=
‚ü®linear_map.comp g.to_linear_map f.to_linear_map, g.2.comp f.2‚ü©

@[simp, norm_cast] lemma coe_comp : ((h.comp f) : (M ‚Üí‚Çó[R] M‚ÇÉ)) = (h : M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ).comp f := rfl
@[simp, norm_cast] lemma coe_comp' : ((h.comp f) : (M ‚Üí M‚ÇÉ)) = (h : M‚ÇÇ ‚Üí M‚ÇÉ) ‚àò f := rfl

@[simp] theorem comp_id : f.comp (id R M) = f :=
ext $ Œª x, rfl

@[simp] theorem id_comp : (id R M‚ÇÇ).comp f = f :=
ext $ Œª x, rfl

@[simp] theorem comp_zero : f.comp (0 : M‚ÇÉ ‚ÜíL[R] M) = 0 :=
by { ext, simp }

@[simp] theorem zero_comp : (0 : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ).comp f = 0 :=
by { ext, simp }

@[simp] lemma comp_add [has_continuous_add M‚ÇÇ] [has_continuous_add M‚ÇÉ]
  (g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (f‚ÇÅ f‚ÇÇ : M ‚ÜíL[R] M‚ÇÇ) :
  g.comp (f‚ÇÅ + f‚ÇÇ) = g.comp f‚ÇÅ + g.comp f‚ÇÇ :=
by { ext, simp }

@[simp] lemma add_comp [has_continuous_add M‚ÇÉ]
  (g‚ÇÅ g‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (f : M ‚ÜíL[R] M‚ÇÇ) :
  (g‚ÇÅ + g‚ÇÇ).comp f = g‚ÇÅ.comp f + g‚ÇÇ.comp f :=
by { ext, simp }

theorem comp_assoc (h : M‚ÇÉ ‚ÜíL[R] M‚ÇÑ) (g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (f : M ‚ÜíL[R] M‚ÇÇ) :
  (h.comp g).comp f = h.comp (g.comp f) :=
rfl

instance : has_mul (M ‚ÜíL[R] M) := ‚ü®comp‚ü©

lemma mul_def (f g : M ‚ÜíL[R] M) : f * g = f.comp g := rfl

@[simp] lemma coe_mul (f g : M ‚ÜíL[R] M) : ‚áë(f * g) = f ‚àò g := rfl

lemma mul_apply (f g : M ‚ÜíL[R] M) (x : M) : (f * g) x = f (g x) := rfl

/-- The cartesian product of two bounded linear maps, as a bounded linear map. -/
protected def prod (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M ‚ÜíL[R] M‚ÇÉ) : M ‚ÜíL[R] (M‚ÇÇ √ó M‚ÇÉ) :=
{ cont := f‚ÇÅ.2.prod_mk f‚ÇÇ.2,
  ..f‚ÇÅ.to_linear_map.prod f‚ÇÇ.to_linear_map }

@[simp, norm_cast] lemma coe_prod (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M ‚ÜíL[R] M‚ÇÉ) :
  (f‚ÇÅ.prod f‚ÇÇ : M ‚Üí‚Çó[R] M‚ÇÇ √ó M‚ÇÉ) = linear_map.prod f‚ÇÅ f‚ÇÇ :=
rfl

@[simp, norm_cast] lemma prod_apply (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M ‚ÜíL[R] M‚ÇÉ) (x : M) :
  f‚ÇÅ.prod f‚ÇÇ x = (f‚ÇÅ x, f‚ÇÇ x) :=
rfl

/-- Kernel of a continuous linear map. -/
def ker (f : M ‚ÜíL[R] M‚ÇÇ) : submodule R M := (f : M ‚Üí‚Çó[R] M‚ÇÇ).ker

@[norm_cast] lemma ker_coe : (f : M ‚Üí‚Çó[R] M‚ÇÇ).ker = f.ker := rfl

@[simp] lemma mem_ker {f : M ‚ÜíL[R] M‚ÇÇ} {x} : x ‚àà f.ker ‚Üî f x = 0 := linear_map.mem_ker

lemma is_closed_ker [t1_space M‚ÇÇ] : is_closed (f.ker : set M) :=
continuous_iff_is_closed.1 f.cont _ is_closed_singleton

@[simp] lemma apply_ker (x : f.ker) : f x = 0 := mem_ker.1 x.2

lemma is_complete_ker {M' : Type*} [uniform_space M'] [complete_space M'] [add_comm_monoid M']
  [semimodule R M'] [t1_space M‚ÇÇ] (f : M' ‚ÜíL[R] M‚ÇÇ) :
  is_complete (f.ker : set M') :=
f.is_closed_ker.is_complete

instance complete_space_ker {M' : Type*} [uniform_space M'] [complete_space M'] [add_comm_monoid M']
  [semimodule R M'] [t1_space M‚ÇÇ] (f : M' ‚ÜíL[R] M‚ÇÇ) :
  complete_space f.ker :=
f.is_closed_ker.complete_space_coe

@[simp] lemma ker_prod (f : M ‚ÜíL[R] M‚ÇÇ) (g : M ‚ÜíL[R] M‚ÇÉ) :
  ker (f.prod g) = ker f ‚äì ker g :=
linear_map.ker_prod f g

/-- Range of a continuous linear map. -/
def range (f : M ‚ÜíL[R] M‚ÇÇ) : submodule R M‚ÇÇ := (f : M ‚Üí‚Çó[R] M‚ÇÇ).range

lemma range_coe : (f.range : set M‚ÇÇ) = set.range f := linear_map.range_coe _
lemma mem_range {f : M ‚ÜíL[R] M‚ÇÇ} {y} : y ‚àà f.range ‚Üî ‚àÉ x, f x = y := linear_map.mem_range

lemma range_prod_le (f : M ‚ÜíL[R] M‚ÇÇ) (g : M ‚ÜíL[R] M‚ÇÉ) :
  range (f.prod g) ‚â§ (range f).prod (range g) :=
(f : M ‚Üí‚Çó[R] M‚ÇÇ).range_prod_le g

/-- Restrict codomain of a continuous linear map. -/
def cod_restrict (f : M ‚ÜíL[R] M‚ÇÇ) (p : submodule R M‚ÇÇ) (h : ‚àÄ x, f x ‚àà p) :
  M ‚ÜíL[R] p :=
{ cont := continuous_subtype_mk h f.continuous,
  to_linear_map := (f : M ‚Üí‚Çó[R] M‚ÇÇ).cod_restrict p h}

@[norm_cast] lemma coe_cod_restrict (f : M ‚ÜíL[R] M‚ÇÇ) (p : submodule R M‚ÇÇ) (h : ‚àÄ x, f x ‚àà p) :
  (f.cod_restrict p h : M ‚Üí‚Çó[R] p) = (f : M ‚Üí‚Çó[R] M‚ÇÇ).cod_restrict p h :=
rfl

@[simp] lemma coe_cod_restrict_apply (f : M ‚ÜíL[R] M‚ÇÇ) (p : submodule R M‚ÇÇ) (h : ‚àÄ x, f x ‚àà p) (x) :
  (f.cod_restrict p h x : M‚ÇÇ) = f x :=
rfl

@[simp] lemma ker_cod_restrict (f : M ‚ÜíL[R] M‚ÇÇ) (p : submodule R M‚ÇÇ) (h : ‚àÄ x, f x ‚àà p) :
  ker (f.cod_restrict p h) = ker f :=
(f : M ‚Üí‚Çó[R] M‚ÇÇ).ker_cod_restrict p h

/-- Embedding of a submodule into the ambient space as a continuous linear map. -/
def subtype_val (p : submodule R M) : p ‚ÜíL[R] M :=
{ cont := continuous_subtype_val,
  to_linear_map := p.subtype }

@[simp, norm_cast] lemma coe_subtype_val (p : submodule R M) :
  (subtype_val p : p ‚Üí‚Çó[R] M) = p.subtype :=
rfl

@[simp, norm_cast] lemma subtype_val_apply (p : submodule R M) (x : p) :
  (subtype_val p : p ‚Üí M) x = x :=
rfl

variables (R M M‚ÇÇ)

/-- `prod.fst` as a `continuous_linear_map`. -/
def fst : M √ó M‚ÇÇ ‚ÜíL[R] M :=
{ cont := continuous_fst, to_linear_map := linear_map.fst R M M‚ÇÇ }

/-- `prod.snd` as a `continuous_linear_map`. -/
def snd : M √ó M‚ÇÇ ‚ÜíL[R] M‚ÇÇ :=
{ cont := continuous_snd, to_linear_map := linear_map.snd R M M‚ÇÇ }

variables {R M M‚ÇÇ}

@[simp, norm_cast] lemma coe_fst : (fst R M M‚ÇÇ : M √ó M‚ÇÇ ‚Üí‚Çó[R] M) = linear_map.fst R M M‚ÇÇ := rfl

@[simp, norm_cast] lemma coe_fst' : (fst R M M‚ÇÇ : M √ó M‚ÇÇ ‚Üí M) = prod.fst := rfl

@[simp, norm_cast] lemma coe_snd : (snd R M M‚ÇÇ : M √ó M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÇ) = linear_map.snd R M M‚ÇÇ := rfl

@[simp, norm_cast] lemma coe_snd' : (snd R M M‚ÇÇ : M √ó M‚ÇÇ ‚Üí M‚ÇÇ) = prod.snd := rfl

@[simp] lemma fst_prod_snd : (fst R M M‚ÇÇ).prod (snd R M M‚ÇÇ) = id R (M √ó M‚ÇÇ) := ext $ Œª ‚ü®x, y‚ü©, rfl

/-- `prod.map` of two continuous linear maps. -/
def prod_map (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÉ ‚ÜíL[R] M‚ÇÑ) : (M √ó M‚ÇÉ) ‚ÜíL[R] (M‚ÇÇ √ó M‚ÇÑ) :=
(f‚ÇÅ.comp (fst R M M‚ÇÉ)).prod (f‚ÇÇ.comp (snd R M M‚ÇÉ))

@[simp, norm_cast] lemma coe_prod_map (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÉ ‚ÜíL[R] M‚ÇÑ) :
  (f‚ÇÅ.prod_map f‚ÇÇ : (M √ó M‚ÇÉ) ‚Üí‚Çó[R] (M‚ÇÇ √ó M‚ÇÑ)) = ((f‚ÇÅ : M ‚Üí‚Çó[R] M‚ÇÇ).prod_map (f‚ÇÇ : M‚ÇÉ ‚Üí‚Çó[R] M‚ÇÑ)) :=
rfl

@[simp, norm_cast] lemma coe_prod_map' (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÉ ‚ÜíL[R] M‚ÇÑ) :
  ‚áë(f‚ÇÅ.prod_map f‚ÇÇ) = prod.map f‚ÇÅ f‚ÇÇ :=
rfl

/-- The continuous linear map given by `(x, y) ‚Ü¶ f‚ÇÅ x + f‚ÇÇ y`. -/
def coprod [has_continuous_add M‚ÇÉ] (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÉ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) :
  (M √ó M‚ÇÇ) ‚ÜíL[R] M‚ÇÉ :=
‚ü®linear_map.coprod f‚ÇÅ f‚ÇÇ, (f‚ÇÅ.cont.comp continuous_fst).add (f‚ÇÇ.cont.comp continuous_snd)‚ü©

@[norm_cast, simp] lemma coe_coprod [has_continuous_add M‚ÇÉ]
  (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÉ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) :
  (f‚ÇÅ.coprod f‚ÇÇ : (M √ó M‚ÇÇ) ‚Üí‚Çó[R] M‚ÇÉ) = linear_map.coprod f‚ÇÅ f‚ÇÇ :=
rfl

@[simp] lemma coprod_apply [has_continuous_add M‚ÇÉ] (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÉ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (x) :
  f‚ÇÅ.coprod f‚ÇÇ x = f‚ÇÅ x.1 + f‚ÇÇ x.2 := rfl

variables [topological_space R] [topological_semimodule R M‚ÇÇ]

/-- The linear map `Œª x, c x ‚Ä¢ f`.  Associates to a scalar-valued linear map and an element of
`M‚ÇÇ` the `M‚ÇÇ`-valued linear map obtained by multiplying the two (a.k.a. tensoring by `M‚ÇÇ`).
See also `continuous_linear_map.smul_right‚Çó` and `continuous_linear_map.smul_rightL`. -/
def smul_right (c : M ‚ÜíL[R] R) (f : M‚ÇÇ) : M ‚ÜíL[R] M‚ÇÇ :=
{ cont := c.2.smul continuous_const,
  ..c.to_linear_map.smul_right f }

@[simp]
lemma smul_right_apply {c : M ‚ÜíL[R] R} {f : M‚ÇÇ} {x : M} :
  (smul_right c f : M ‚Üí M‚ÇÇ) x = (c : M ‚Üí R) x ‚Ä¢ f :=
rfl

@[simp]
lemma smul_right_one_one (c : R ‚ÜíL[R] M‚ÇÇ) : smul_right 1 ((c : R ‚Üí M‚ÇÇ) 1) = c :=
by ext; simp [-continuous_linear_map.map_smul, (continuous_linear_map.map_smul _ _ _).symm]

@[simp]
lemma smul_right_one_eq_iff {f f' : M‚ÇÇ} :
  smul_right (1 : R ‚ÜíL[R] R) f = smul_right 1 f' ‚Üî f = f' :=
‚ü®Œª h, have (smul_right (1 : R ‚ÜíL[R] R) f : R ‚Üí M‚ÇÇ) 1 = (smul_right (1 : R ‚ÜíL[R] R) f' : R ‚Üí M‚ÇÇ) 1,
        by rw h,
      by simp at this; assumption,
  by cc‚ü©

lemma smul_right_comp [topological_semimodule R R] {x : M‚ÇÇ} {c : R} :
  (smul_right 1 x : R ‚ÜíL[R] M‚ÇÇ).comp (smul_right 1 c : R ‚ÜíL[R] R) = smul_right 1 (c ‚Ä¢ x) :=
by { ext, simp [mul_smul] }

end semiring

section pi
variables
  {R : Type*} [semiring R]
  {M : Type*} [topological_space M] [add_comm_monoid M] [semimodule R M]
  {M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_monoid M‚ÇÇ] [semimodule R M‚ÇÇ]
  {Œπ : Type*} {œÜ : Œπ ‚Üí Type*} [‚àÄi, topological_space (œÜ i)] [‚àÄi, add_comm_monoid (œÜ i)] [‚àÄi, semimodule R (œÜ i)]

/-- `pi` construction for continuous linear functions. From a family of continuous linear functions
it produces a continuous linear function into a family of topological modules. -/
def pi (f : Œ†i, M ‚ÜíL[R] œÜ i) : M ‚ÜíL[R] (Œ†i, œÜ i) :=
‚ü®linear_map.pi (Œª i, (f i : M ‚Üí‚Çó[R] œÜ i)),
 continuous_pi (Œª i, (f i).continuous)‚ü©

@[simp] lemma pi_apply (f : Œ†i, M ‚ÜíL[R] œÜ i) (c : M) (i : Œπ) :
  pi f c i = f i c := rfl

lemma pi_eq_zero (f : Œ†i, M ‚ÜíL[R] œÜ i) : pi f = 0 ‚Üî (‚àÄi, f i = 0) :=
by simp only [ext_iff, pi_apply, function.funext_iff]; exact ‚ü®Œªh a b, h b a, Œªh a b, h b a‚ü©

lemma pi_zero : pi (Œªi, 0 : Œ†i, M ‚ÜíL[R] œÜ i) = 0 := by ext; refl

lemma pi_comp (f : Œ†i, M ‚ÜíL[R] œÜ i) (g : M‚ÇÇ ‚ÜíL[R] M) : (pi f).comp g = pi (Œªi, (f i).comp g) := rfl

/-- The projections from a family of topological modules are continuous linear maps. -/
def proj (i : Œπ) : (Œ†i, œÜ i) ‚ÜíL[R] œÜ i :=
‚ü®linear_map.proj i, continuous_apply _‚ü©

@[simp] lemma proj_apply (i : Œπ) (b : Œ†i, œÜ i) : (proj i : (Œ†i, œÜ i) ‚ÜíL[R] œÜ i) b = b i := rfl

lemma proj_pi (f : Œ†i, M‚ÇÇ ‚ÜíL[R] œÜ i) (i : Œπ) : (proj i).comp (pi f) = f i :=
ext $ assume c, rfl

lemma infi_ker_proj : (‚®Öi, ker (proj i) : submodule R (Œ†i, œÜ i)) = ‚ä• :=
linear_map.infi_ker_proj

variables (R œÜ)

/-- If `I` and `J` are complementary index sets, the product of the kernels of the `J`th projections of
`œÜ` is linearly equivalent to the product over `I`. -/
def infi_ker_proj_equiv {I J : set Œπ} [decidable_pred (Œªi, i ‚àà I)]
  (hd : disjoint I J) (hu : set.univ ‚äÜ I ‚à™ J) :
  (‚®Öi ‚àà J, ker (proj i) : submodule R (Œ†i, œÜ i)) ‚âÉL[R] (Œ†i:I, œÜ i) :=
‚ü® linear_map.infi_ker_proj_equiv R œÜ hd hu,
  continuous_pi (Œª i, begin
    have := @continuous_subtype_coe _ _ (Œª x, x ‚àà (‚®Öi ‚àà J, ker (proj i) : submodule R (Œ†i, œÜ i))),
    have := continuous.comp (by exact continuous_apply i) this,
    exact this
  end),
  continuous_subtype_mk _ (continuous_pi (Œª i, begin
    dsimp, split_ifs; [apply continuous_apply, exact continuous_const]
  end)) ‚ü©

end pi

section ring

variables
{R : Type*} [ring R]
{M : Type*} [topological_space M] [add_comm_group M]
{M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_group M‚ÇÇ]
{M‚ÇÉ : Type*} [topological_space M‚ÇÉ] [add_comm_group M‚ÇÉ]
{M‚ÇÑ : Type*} [topological_space M‚ÇÑ] [add_comm_group M‚ÇÑ]
[semimodule R M] [semimodule R M‚ÇÇ] [semimodule R M‚ÇÉ] [semimodule R M‚ÇÑ]

variables (c : R) (f g : M ‚ÜíL[R] M‚ÇÇ) (h : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (x y z : M)

@[simp] lemma map_neg  : f (-x) = - (f x) := (to_linear_map _).map_neg _
@[simp] lemma map_sub  : f (x - y) = f x - f y := (to_linear_map _).map_sub _ _
@[simp] lemma sub_apply' (x : M) : ((f : M ‚Üí‚Çó[R] M‚ÇÇ) - g) x = f x - g x := rfl

lemma range_prod_eq {f : M ‚ÜíL[R] M‚ÇÇ} {g : M ‚ÜíL[R] M‚ÇÉ} (h : ker f ‚äî ker g = ‚ä§) :
  range (f.prod g) = (range f).prod (range g) :=
linear_map.range_prod_eq h

section
variables [topological_add_group M‚ÇÇ]

instance : has_neg (M ‚ÜíL[R] M‚ÇÇ) := ‚ü®Œª f, ‚ü®-f, f.2.neg‚ü©‚ü©

@[simp] lemma neg_apply : (-f) x = - (f x) := rfl

@[simp, norm_cast] lemma coe_neg : (((-f) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí‚Çó[R] M‚ÇÇ) = -(f : M ‚Üí‚Çó[R] M‚ÇÇ) := rfl
@[norm_cast] lemma coe_neg' : (((-f) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) = -(f : M ‚Üí M‚ÇÇ) := rfl

instance : add_comm_group (M ‚ÜíL[R] M‚ÇÇ) :=
by { refine {zero := 0, add := (+), neg := has_neg.neg, ..}; intros; ext;
  apply_rules [zero_add, add_assoc, add_zero, add_left_neg, add_comm] }

lemma sub_apply (x : M) : (f - g) x = f x - g x := rfl
@[simp, norm_cast] lemma coe_sub : (((f - g) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí‚Çó[R] M‚ÇÇ) = (f : M ‚Üí‚Çó[R] M‚ÇÇ) - g := rfl
@[simp, norm_cast] lemma coe_sub' : (((f - g) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) = (f : M ‚Üí M‚ÇÇ) - g := rfl

end

instance [topological_add_group M] : ring (M ‚ÜíL[R] M) :=
{ mul := (*),
  one := 1,
  mul_one := Œª _, ext $ Œª _, rfl,
  one_mul := Œª _, ext $ Œª _, rfl,
  mul_assoc := Œª _ _ _, ext $ Œª _, rfl,
  left_distrib := Œª _ _ _, ext $ Œª _, map_add _ _ _,
  right_distrib := Œª _ _ _, ext $ Œª _, linear_map.add_apply _ _ _,
  ..continuous_linear_map.add_comm_group }

lemma smul_right_one_pow [topological_space R]
  [topological_add_group R] [topological_semimodule R R] (c : R) (n : ‚Ñï) :
  (smul_right 1 c : R ‚ÜíL[R] R)^n = smul_right 1 (c^n) :=
begin
  induction n with n ihn,
  { ext, simp },
  { rw [pow_succ, ihn, mul_def, smul_right_comp, smul_eq_mul, pow_succ'] }
end

/-- Given a right inverse `f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M` to `f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ`,
`proj_ker_of_right_inverse f‚ÇÅ f‚ÇÇ h` is the projection `M ‚ÜíL[R] f‚ÇÅ.ker` along `f‚ÇÇ.range`. -/
def proj_ker_of_right_inverse [topological_add_group M] (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
  (h : function.right_inverse f‚ÇÇ f‚ÇÅ) :
  M ‚ÜíL[R] f‚ÇÅ.ker :=
(id R M - f‚ÇÇ.comp f‚ÇÅ).cod_restrict f‚ÇÅ.ker $ Œª x, by simp [h (f‚ÇÅ x)]

@[simp] lemma coe_proj_ker_of_right_inverse_apply [topological_add_group M]
  (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M) (h : function.right_inverse f‚ÇÇ f‚ÇÅ) (x : M) :
  (f‚ÇÅ.proj_ker_of_right_inverse f‚ÇÇ h x : M) = x - f‚ÇÇ (f‚ÇÅ x) :=
rfl

@[simp] lemma proj_ker_of_right_inverse_apply_idem [topological_add_group M]
  (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M) (h : function.right_inverse f‚ÇÇ f‚ÇÅ) (x : f‚ÇÅ.ker) :
  f‚ÇÅ.proj_ker_of_right_inverse f‚ÇÇ h x = x :=
subtype.ext_iff_val.2 $ by simp

@[simp] lemma proj_ker_of_right_inverse_comp_inv [topological_add_group M]
  (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M) (h : function.right_inverse f‚ÇÇ f‚ÇÅ) (y : M‚ÇÇ) :
  f‚ÇÅ.proj_ker_of_right_inverse f‚ÇÇ h (f‚ÇÇ y) = 0 :=
subtype.ext_iff_val.2 $ by simp [h y]

end ring

section comm_ring

variables
{R : Type*} [comm_ring R] [topological_space R]
{M : Type*} [topological_space M] [add_comm_group M]
{M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_group M‚ÇÇ]
{M‚ÇÉ : Type*} [topological_space M‚ÇÉ] [add_comm_group M‚ÇÉ]
[module R M] [module R M‚ÇÇ] [module R M‚ÇÉ] [topological_module R M‚ÇÉ]

instance : has_scalar R (M ‚ÜíL[R] M‚ÇÉ) :=
‚ü®Œª c f, ‚ü®c ‚Ä¢ f, continuous_const.smul f.2‚ü©‚ü©

variables (c : R) (h : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (f g : M ‚ÜíL[R] M‚ÇÇ) (x y z : M)

@[simp] lemma smul_comp : (c ‚Ä¢ h).comp f = c ‚Ä¢ (h.comp f) := rfl

variable [topological_module R M‚ÇÇ]

@[simp] lemma smul_apply : (c ‚Ä¢ f) x = c ‚Ä¢ (f x) := rfl
@[simp, norm_cast] lemma coe_apply : (((c ‚Ä¢ f) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí‚Çó[R] M‚ÇÇ) = c ‚Ä¢ (f : M ‚Üí‚Çó[R] M‚ÇÇ) := rfl
@[norm_cast] lemma coe_apply' : (((c ‚Ä¢ f) : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) = c ‚Ä¢ (f : M ‚Üí M‚ÇÇ) := rfl

@[simp] lemma comp_smul : h.comp (c ‚Ä¢ f) = c ‚Ä¢ (h.comp f) := by { ext, simp }

variable [topological_add_group M‚ÇÇ]

instance : module R (M ‚ÜíL[R] M‚ÇÇ) :=
{ smul_zero := Œª _, ext $ Œª _, smul_zero _,
  zero_smul := Œª _, ext $ Œª _, zero_smul _ _,
  one_smul  := Œª _, ext $ Œª _, one_smul _ _,
  mul_smul  := Œª _ _ _, ext $ Œª _, mul_smul _ _ _,
  add_smul  := Œª _ _ _, ext $ Œª _, add_smul _ _ _,
  smul_add  := Œª _ _ _, ext $ Œª _, smul_add _ _ _ }

instance : algebra R (M‚ÇÇ ‚ÜíL[R] M‚ÇÇ) :=
algebra.of_semimodule' (Œª c f, ext $ Œª x, rfl) (Œª c f, ext $ Œª x, f.map_smul c x)

/-- Given `c : E ‚ÜíL[ùïú] ùïú`, `c.smul_right‚Çó` is the linear map from `F` to `E ‚ÜíL[ùïú] F`
sending `f` to `Œª e, c e ‚Ä¢ f`. See also `continuous_linear_map.smul_rightL`. -/
def smul_right‚Çó (c : M ‚ÜíL[R] R) : M‚ÇÇ ‚Üí‚Çó[R] (M ‚ÜíL[R] M‚ÇÇ) :=
{ to_fun := c.smul_right,
  map_add' := Œª x y, by { ext e, simp [smul_add] },
  map_smul' := Œª a x, by { ext e, simp [smul_comm] } }

end comm_ring

end continuous_linear_map

namespace continuous_linear_equiv

section add_comm_monoid

variables {R : Type*} [semiring R]
{M : Type*} [topological_space M] [add_comm_monoid M]
{M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_monoid M‚ÇÇ]
{M‚ÇÉ : Type*} [topological_space M‚ÇÉ] [add_comm_monoid M‚ÇÉ]
{M‚ÇÑ : Type*} [topological_space M‚ÇÑ] [add_comm_monoid M‚ÇÑ]
[semimodule R M] [semimodule R M‚ÇÇ] [semimodule R M‚ÇÉ] [semimodule R M‚ÇÑ]

/-- A continuous linear equivalence induces a continuous linear map. -/
def to_continuous_linear_map (e : M ‚âÉL[R] M‚ÇÇ) : M ‚ÜíL[R] M‚ÇÇ :=
{ cont := e.continuous_to_fun,
  ..e.to_linear_equiv.to_linear_map }

/-- Coerce continuous linear equivs to continuous linear maps. -/
instance : has_coe (M ‚âÉL[R] M‚ÇÇ) (M ‚ÜíL[R] M‚ÇÇ) := ‚ü®to_continuous_linear_map‚ü©

/-- Coerce continuous linear equivs to maps. -/
-- see Note [function coercion]
instance : has_coe_to_fun (M ‚âÉL[R] M‚ÇÇ) := ‚ü®Œª _, M ‚Üí M‚ÇÇ, Œª f, f‚ü©

@[simp] theorem coe_def_rev (e : M ‚âÉL[R] M‚ÇÇ) : e.to_continuous_linear_map = e := rfl

@[simp] theorem coe_apply (e : M ‚âÉL[R] M‚ÇÇ) (b : M) : (e : M ‚ÜíL[R] M‚ÇÇ) b = e b := rfl

@[norm_cast] lemma coe_coe (e : M ‚âÉL[R] M‚ÇÇ) : ((e : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) = e := rfl

@[ext] lemma ext {f g : M ‚âÉL[R] M‚ÇÇ} (h : (f : M ‚Üí M‚ÇÇ) = g) : f = g :=
begin
  cases f; cases g,
  simp only,
  ext x,
  induction h,
  refl
end

/-- A continuous linear equivalence induces a homeomorphism. -/
def to_homeomorph (e : M ‚âÉL[R] M‚ÇÇ) : M ‚âÉ‚Çú M‚ÇÇ := { ..e }

-- Make some straightforward lemmas available to `simp`.
@[simp] lemma map_zero (e : M ‚âÉL[R] M‚ÇÇ) : e (0 : M) = 0 := (e : M ‚ÜíL[R] M‚ÇÇ).map_zero
@[simp] lemma map_add (e : M ‚âÉL[R] M‚ÇÇ) (x y : M) : e (x + y) = e x + e y :=
(e : M ‚ÜíL[R] M‚ÇÇ).map_add x y
@[simp] lemma map_smul (e : M ‚âÉL[R] M‚ÇÇ) (c : R) (x : M) : e (c ‚Ä¢ x) = c ‚Ä¢ (e x) :=
(e : M ‚ÜíL[R] M‚ÇÇ).map_smul c x
@[simp] lemma map_eq_zero_iff (e : M ‚âÉL[R] M‚ÇÇ) {x : M} : e x = 0 ‚Üî x = 0 :=
e.to_linear_equiv.map_eq_zero_iff

attribute [continuity]
  continuous_linear_equiv.continuous_to_fun continuous_linear_equiv.continuous_inv_fun

@[continuity]
protected lemma continuous (e : M ‚âÉL[R] M‚ÇÇ) : continuous (e : M ‚Üí M‚ÇÇ) :=
e.continuous_to_fun

protected lemma continuous_on (e : M ‚âÉL[R] M‚ÇÇ) {s : set M} : continuous_on (e : M ‚Üí M‚ÇÇ) s :=
e.continuous.continuous_on

protected lemma continuous_at (e : M ‚âÉL[R] M‚ÇÇ) {x : M} : continuous_at (e : M ‚Üí M‚ÇÇ) x :=
e.continuous.continuous_at

protected lemma continuous_within_at (e : M ‚âÉL[R] M‚ÇÇ) {s : set M} {x : M} :
  continuous_within_at (e : M ‚Üí M‚ÇÇ) s x :=
e.continuous.continuous_within_at

lemma comp_continuous_on_iff
  {Œ± : Type*} [topological_space Œ±] (e : M ‚âÉL[R] M‚ÇÇ) (f : Œ± ‚Üí M) (s : set Œ±) :
  continuous_on (e ‚àò f) s ‚Üî continuous_on f s :=
e.to_homeomorph.comp_continuous_on_iff _ _

lemma comp_continuous_iff
  {Œ± : Type*} [topological_space Œ±] (e : M ‚âÉL[R] M‚ÇÇ) (f : Œ± ‚Üí M) :
  continuous (e ‚àò f) ‚Üî continuous f :=
e.to_homeomorph.comp_continuous_iff _

/-- An extensionality lemma for `R ‚âÉL[R] M`. -/
lemma ext‚ÇÅ [topological_space R] {f g : R ‚âÉL[R] M} (h : f 1 = g 1) : f = g :=
ext $ funext $ Œª x, mul_one x ‚ñ∏ by rw [‚Üê smul_eq_mul, map_smul, h, map_smul]

section
variables (R M)

/-- The identity map as a continuous linear equivalence. -/
@[refl] protected def refl : M ‚âÉL[R] M :=
{ continuous_to_fun := continuous_id,
  continuous_inv_fun := continuous_id,
  .. linear_equiv.refl R M }
end

@[simp, norm_cast] lemma coe_refl :
  (continuous_linear_equiv.refl R M : M ‚ÜíL[R] M) = continuous_linear_map.id R M := rfl

@[simp, norm_cast] lemma coe_refl' :
  (continuous_linear_equiv.refl R M : M ‚Üí M) = id := rfl

/-- The inverse of a continuous linear equivalence as a continuous linear equivalence-/
@[symm] protected def symm (e : M ‚âÉL[R] M‚ÇÇ) : M‚ÇÇ ‚âÉL[R] M :=
{ continuous_to_fun := e.continuous_inv_fun,
  continuous_inv_fun := e.continuous_to_fun,
  .. e.to_linear_equiv.symm }
@[simp] lemma symm_to_linear_equiv (e : M ‚âÉL[R] M‚ÇÇ) :
  e.symm.to_linear_equiv = e.to_linear_equiv.symm :=
by { ext, refl }

/-- The composition of two continuous linear equivalences as a continuous linear equivalence. -/
@[trans] protected def trans (e‚ÇÅ : M ‚âÉL[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ ‚âÉL[R] M‚ÇÉ) : M ‚âÉL[R] M‚ÇÉ :=
{ continuous_to_fun := e‚ÇÇ.continuous_to_fun.comp e‚ÇÅ.continuous_to_fun,
  continuous_inv_fun := e‚ÇÅ.continuous_inv_fun.comp e‚ÇÇ.continuous_inv_fun,
  .. e‚ÇÅ.to_linear_equiv.trans e‚ÇÇ.to_linear_equiv }
@[simp] lemma trans_to_linear_equiv (e‚ÇÅ : M ‚âÉL[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ ‚âÉL[R] M‚ÇÉ) :
  (e‚ÇÅ.trans e‚ÇÇ).to_linear_equiv = e‚ÇÅ.to_linear_equiv.trans e‚ÇÇ.to_linear_equiv :=
by { ext, refl }

/-- Product of two continuous linear equivalences. The map comes from `equiv.prod_congr`. -/
def prod (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) : (M √ó M‚ÇÉ) ‚âÉL[R] (M‚ÇÇ √ó M‚ÇÑ) :=
{ continuous_to_fun := e.continuous_to_fun.prod_map e'.continuous_to_fun,
  continuous_inv_fun := e.continuous_inv_fun.prod_map e'.continuous_inv_fun,
  .. e.to_linear_equiv.prod e'.to_linear_equiv }
@[simp, norm_cast] lemma prod_apply (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) (x) :
  e.prod e' x = (e x.1, e' x.2) := rfl

@[simp, norm_cast] lemma coe_prod (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) :
  (e.prod e' : (M √ó M‚ÇÉ) ‚ÜíL[R] (M‚ÇÇ √ó M‚ÇÑ)) = (e : M ‚ÜíL[R] M‚ÇÇ).prod_map (e' : M‚ÇÉ ‚ÜíL[R] M‚ÇÑ) :=
rfl

theorem bijective (e : M ‚âÉL[R] M‚ÇÇ) : function.bijective e := e.to_linear_equiv.to_equiv.bijective
theorem injective (e : M ‚âÉL[R] M‚ÇÇ) : function.injective e := e.to_linear_equiv.to_equiv.injective
theorem surjective (e : M ‚âÉL[R] M‚ÇÇ) : function.surjective e := e.to_linear_equiv.to_equiv.surjective

@[simp] theorem trans_apply (e‚ÇÅ : M ‚âÉL[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ ‚âÉL[R] M‚ÇÉ) (c : M) :
  (e‚ÇÅ.trans e‚ÇÇ) c = e‚ÇÇ (e‚ÇÅ c) :=
rfl
@[simp] theorem apply_symm_apply (e : M ‚âÉL[R] M‚ÇÇ) (c : M‚ÇÇ) : e (e.symm c) = c := e.1.6 c
@[simp] theorem symm_apply_apply (e : M ‚âÉL[R] M‚ÇÇ) (b : M) : e.symm (e b) = b := e.1.5 b
@[simp] theorem symm_trans_apply (e‚ÇÅ : M‚ÇÇ ‚âÉL[R] M) (e‚ÇÇ : M‚ÇÉ ‚âÉL[R] M‚ÇÇ) (c : M) :
  (e‚ÇÇ.trans e‚ÇÅ).symm c = e‚ÇÇ.symm (e‚ÇÅ.symm c) :=
rfl

@[simp, norm_cast]
lemma comp_coe (f : M ‚âÉL[R] M‚ÇÇ) (f' : M‚ÇÇ ‚âÉL[R] M‚ÇÉ) :
  (f' : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ).comp (f : M ‚ÜíL[R] M‚ÇÇ) = (f.trans f' : M ‚ÜíL[R] M‚ÇÉ) :=
rfl

@[simp] theorem coe_comp_coe_symm (e : M ‚âÉL[R] M‚ÇÇ) :
  (e : M ‚ÜíL[R] M‚ÇÇ).comp (e.symm : M‚ÇÇ ‚ÜíL[R] M) = continuous_linear_map.id R M‚ÇÇ :=
continuous_linear_map.ext e.apply_symm_apply

@[simp] theorem coe_symm_comp_coe (e : M ‚âÉL[R] M‚ÇÇ) :
  (e.symm : M‚ÇÇ ‚ÜíL[R] M).comp (e : M ‚ÜíL[R] M‚ÇÇ) = continuous_linear_map.id R M :=
continuous_linear_map.ext e.symm_apply_apply

lemma symm_comp_self (e : M ‚âÉL[R] M‚ÇÇ) :
  (e.symm : M‚ÇÇ ‚Üí M) ‚àò (e : M ‚Üí M‚ÇÇ) = id :=
by{ ext x, exact symm_apply_apply e x }

lemma self_comp_symm (e : M ‚âÉL[R] M‚ÇÇ) :
  (e : M ‚Üí M‚ÇÇ) ‚àò (e.symm : M‚ÇÇ ‚Üí M) = id :=
by{ ext x, exact apply_symm_apply e x }

@[simp] lemma symm_comp_self' (e : M ‚âÉL[R] M‚ÇÇ) :
  ((e.symm : M‚ÇÇ ‚ÜíL[R] M) : M‚ÇÇ ‚Üí M) ‚àò ((e : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) = id :=
symm_comp_self e

@[simp] lemma self_comp_symm' (e : M ‚âÉL[R] M‚ÇÇ) :
  ((e : M ‚ÜíL[R] M‚ÇÇ) : M ‚Üí M‚ÇÇ) ‚àò ((e.symm : M‚ÇÇ ‚ÜíL[R] M) : M‚ÇÇ ‚Üí M) = id :=
self_comp_symm e

@[simp] theorem symm_symm (e : M ‚âÉL[R] M‚ÇÇ) : e.symm.symm = e :=
by { ext x, refl }

@[simp] lemma refl_symm :
 (continuous_linear_equiv.refl R M).symm = continuous_linear_equiv.refl R M :=
rfl

theorem symm_symm_apply (e : M ‚âÉL[R] M‚ÇÇ) (x : M) : e.symm.symm x = e x :=
rfl

lemma symm_apply_eq (e : M ‚âÉL[R] M‚ÇÇ) {x y} : e.symm x = y ‚Üî x = e y :=
e.to_linear_equiv.symm_apply_eq

lemma eq_symm_apply (e : M ‚âÉL[R] M‚ÇÇ) {x y} : y = e.symm x ‚Üî e y = x :=
e.to_linear_equiv.eq_symm_apply

/-- Create a `continuous_linear_equiv` from two `continuous_linear_map`s that are
inverse of each other. -/
def equiv_of_inverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M) (h‚ÇÅ : function.left_inverse f‚ÇÇ f‚ÇÅ)
  (h‚ÇÇ : function.right_inverse f‚ÇÇ f‚ÇÅ) :
  M ‚âÉL[R] M‚ÇÇ :=
{ to_fun := f‚ÇÅ,
  continuous_to_fun := f‚ÇÅ.continuous,
  inv_fun := f‚ÇÇ,
  continuous_inv_fun := f‚ÇÇ.continuous,
  left_inv := h‚ÇÅ,
  right_inv := h‚ÇÇ,
  .. f‚ÇÅ }

@[simp] lemma equiv_of_inverse_apply (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ h‚ÇÅ h‚ÇÇ x) :
  equiv_of_inverse f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ x = f‚ÇÅ x :=
rfl

@[simp] lemma symm_equiv_of_inverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ h‚ÇÅ h‚ÇÇ) :
  (equiv_of_inverse f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ).symm = equiv_of_inverse f‚ÇÇ f‚ÇÅ h‚ÇÇ h‚ÇÅ :=
rfl

variable (M)

/-- The continuous linear equivalences from `M` to itself form a group under composition. -/
instance automorphism_group : group (M ‚âÉL[R] M) :=
{ mul          := Œª f g, g.trans f,
  one          := continuous_linear_equiv.refl R M,
  inv          := Œª f, f.symm,
  mul_assoc    := Œª f g h, by {ext, refl},
  mul_one      := Œª f, by {ext, refl},
  one_mul      := Œª f, by {ext, refl},
  mul_left_inv := Œª f, by {ext, exact f.left_inv x} }

end add_comm_monoid

section add_comm_group

variables {R : Type*} [semiring R]
{M : Type*} [topological_space M] [add_comm_group M]
{M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_group M‚ÇÇ]
{M‚ÇÉ : Type*} [topological_space M‚ÇÉ] [add_comm_group M‚ÇÉ]
{M‚ÇÑ : Type*} [topological_space M‚ÇÑ] [add_comm_group M‚ÇÑ]
[semimodule R M] [semimodule R M‚ÇÇ] [semimodule R M‚ÇÉ] [semimodule R M‚ÇÑ]

variables [topological_add_group M‚ÇÑ]

/-- Equivalence given by a block lower diagonal matrix. `e` and `e'` are diagonal square blocks,
  and `f` is a rectangular block below the diagonal. -/
def skew_prod (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) (f : M ‚ÜíL[R] M‚ÇÑ) :
  (M √ó M‚ÇÉ) ‚âÉL[R] M‚ÇÇ √ó M‚ÇÑ :=
{ continuous_to_fun := (e.continuous_to_fun.comp continuous_fst).prod_mk
    ((e'.continuous_to_fun.comp continuous_snd).add $ f.continuous.comp continuous_fst),
  continuous_inv_fun := (e.continuous_inv_fun.comp continuous_fst).prod_mk
    (e'.continuous_inv_fun.comp $ continuous_snd.sub $ f.continuous.comp $
      e.continuous_inv_fun.comp continuous_fst),
.. e.to_linear_equiv.skew_prod e'.to_linear_equiv ‚Üëf  }
@[simp] lemma skew_prod_apply (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) (f : M ‚ÜíL[R] M‚ÇÑ) (x) :
  e.skew_prod e' f x = (e x.1, e' x.2 + f x.1) := rfl

@[simp] lemma skew_prod_symm_apply (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) (f : M ‚ÜíL[R] M‚ÇÑ) (x) :
  (e.skew_prod e' f).symm x = (e.symm x.1, e'.symm (x.2 - f (e.symm x.1))) := rfl

end add_comm_group

section ring

variables {R : Type*} [ring R]
{M : Type*} [topological_space M] [add_comm_group M] [semimodule R M]
{M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_group M‚ÇÇ] [semimodule R M‚ÇÇ]

@[simp] lemma map_sub (e : M ‚âÉL[R] M‚ÇÇ) (x y : M) : e (x - y) = e x - e y :=
(e : M ‚ÜíL[R] M‚ÇÇ).map_sub x y

@[simp] lemma map_neg (e : M ‚âÉL[R] M‚ÇÇ) (x : M) : e (-x) = -e x := (e : M ‚ÜíL[R] M‚ÇÇ).map_neg x

section
/-! The next theorems cover the identification between `M ‚âÉL[ùïú] M`and the group of units of the ring
`M ‚ÜíL[R] M`. -/
variables [topological_add_group M]

/-- An invertible continuous linear map `f` determines a continuous equivalence from `M` to itself.
-/
def of_unit (f : units (M ‚ÜíL[R] M)) : (M ‚âÉL[R] M) :=
{ to_linear_equiv :=
  { to_fun    := f.val,
    map_add'  := by simp,
    map_smul' := by simp,
    inv_fun   := f.inv,
    left_inv  := Œª x, show (f.inv * f.val) x = x, by {rw f.inv_val, simp},
    right_inv := Œª x, show (f.val * f.inv) x = x, by {rw f.val_inv, simp}, },
  continuous_to_fun  := f.val.continuous,
  continuous_inv_fun := f.inv.continuous }

/-- A continuous equivalence from `M` to itself determines an invertible continuous linear map. -/
def to_unit (f : (M ‚âÉL[R] M)) : units (M ‚ÜíL[R] M) :=
{ val     := f,
  inv     := f.symm,
  val_inv := by {ext, simp},
  inv_val := by {ext, simp} }

variables (R M)

/-- The units of the algebra of continuous `R`-linear endomorphisms of `M` is multiplicatively
equivalent to the type of continuous linear equivalences between `M` and itself. -/
def units_equiv : units (M ‚ÜíL[R] M) ‚âÉ* (M ‚âÉL[R] M) :=
{ to_fun    := of_unit,
  inv_fun   := to_unit,
  left_inv  := Œª f, by {ext, refl},
  right_inv := Œª f, by {ext, refl},
  map_mul'  := Œª x y, by {ext, refl} }

@[simp] lemma units_equiv_apply (f : units (M ‚ÜíL[R] M)) (x : M) :
  (units_equiv R M f) x = f x := rfl

end

section
variables (R) [topological_space R] [topological_module R R]

/-- Continuous linear equivalences `R ‚âÉL[R] R` are enumerated by `units R`. -/
def units_equiv_aut : units R ‚âÉ (R ‚âÉL[R] R) :=
{ to_fun := Œª u, equiv_of_inverse
    (continuous_linear_map.smul_right 1 ‚Üëu)
    (continuous_linear_map.smul_right 1 ‚Üëu‚Åª¬π)
    (Œª x, by simp) (Œª x, by simp),
  inv_fun := Œª e, ‚ü®e 1, e.symm 1,
    by rw [‚Üê smul_eq_mul, ‚Üê map_smul, smul_eq_mul, mul_one, symm_apply_apply],
    by rw [‚Üê smul_eq_mul, ‚Üê map_smul, smul_eq_mul, mul_one, apply_symm_apply]‚ü©,
  left_inv := Œª u, units.ext $ by simp,
  right_inv := Œª e, ext‚ÇÅ $ by simp }

variable {R}

@[simp] lemma units_equiv_aut_apply (u : units R) (x : R) : units_equiv_aut R u x = x * u := rfl

@[simp] lemma units_equiv_aut_apply_symm (u : units R) (x : R) :
  (units_equiv_aut R u).symm x = x * ‚Üëu‚Åª¬π := rfl

@[simp] lemma units_equiv_aut_symm_apply (e : R ‚âÉL[R] R) :
  ‚Üë((units_equiv_aut R).symm e) = e 1 :=
rfl

end

variables [topological_add_group M]

open continuous_linear_map (id fst snd subtype_val mem_ker)

/-- A pair of continuous linear maps such that `f‚ÇÅ ‚àò f‚ÇÇ = id` generates a continuous
linear equivalence `e` between `M` and `M‚ÇÇ √ó f‚ÇÅ.ker` such that `(e x).2 = x` for `x ‚àà f‚ÇÅ.ker`,
`(e x).1 = f‚ÇÅ x`, and `(e (f‚ÇÇ y)).2 = 0`. The map is given by `e x = (f‚ÇÅ x, x - f‚ÇÇ (f‚ÇÅ x))`. -/
def equiv_of_right_inverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M) (h : function.right_inverse f‚ÇÇ f‚ÇÅ) :
  M ‚âÉL[R] M‚ÇÇ √ó f‚ÇÅ.ker :=
equiv_of_inverse (f‚ÇÅ.prod (f‚ÇÅ.proj_ker_of_right_inverse f‚ÇÇ h)) (f‚ÇÇ.coprod (subtype_val f‚ÇÅ.ker))
  (Œª x, by simp)
  (Œª ‚ü®x, y‚ü©, by simp [h x])

@[simp] lemma fst_equiv_of_right_inverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
  (h : function.right_inverse f‚ÇÇ f‚ÇÅ) (x : M) :
  (equiv_of_right_inverse f‚ÇÅ f‚ÇÇ h x).1 = f‚ÇÅ x := rfl

@[simp] lemma snd_equiv_of_right_inverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
  (h : function.right_inverse f‚ÇÇ f‚ÇÅ) (x : M) :
  ((equiv_of_right_inverse f‚ÇÅ f‚ÇÇ h x).2 : M) = x - f‚ÇÇ (f‚ÇÅ x) := rfl

@[simp] lemma equiv_of_right_inverse_symm_apply (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
  (h : function.right_inverse f‚ÇÇ f‚ÇÅ) (y : M‚ÇÇ √ó f‚ÇÅ.ker) :
  (equiv_of_right_inverse f‚ÇÅ f‚ÇÇ h).symm y = f‚ÇÇ y.1 + y.2 := rfl

end ring

end continuous_linear_equiv

namespace continuous_linear_map

open_locale classical

variables {R : Type*} {M : Type*} {M‚ÇÇ : Type*} [topological_space M] [topological_space M‚ÇÇ]

section
variables [semiring R]
variables [add_comm_monoid M‚ÇÇ] [semimodule R M‚ÇÇ]
variables [add_comm_monoid M] [semimodule R M]

/-- Introduce a function `inverse` from `M ‚ÜíL[R] M‚ÇÇ` to `M‚ÇÇ ‚ÜíL[R] M`, which sends `f` to `f.symm` if
`f` is a continuous linear equivalence and to `0` otherwise.  This definition is somewhat ad hoc,
but one needs a fully (rather than partially) defined inverse function for some purposes, including
for calculus. -/
noncomputable def inverse : (M ‚ÜíL[R] M‚ÇÇ) ‚Üí (M‚ÇÇ ‚ÜíL[R] M) :=
Œª f, if h : ‚àÉ (e : M ‚âÉL[R] M‚ÇÇ), (e : M ‚ÜíL[R] M‚ÇÇ) = f then ((classical.some h).symm : M‚ÇÇ ‚ÜíL[R] M) else 0

/-- By definition, if `f` is invertible then `inverse f = f.symm`. -/
@[simp] lemma inverse_equiv (e : M ‚âÉL[R] M‚ÇÇ) : inverse (e : M ‚ÜíL[R] M‚ÇÇ) = e.symm :=
begin
  have h : ‚àÉ (e' : M ‚âÉL[R] M‚ÇÇ), (e' : M ‚ÜíL[R] M‚ÇÇ) = ‚Üëe := ‚ü®e, rfl‚ü©,
  simp only [inverse, dif_pos h],
  congr,
  ext x,
  have h' := classical.some_spec h,
  simpa using continuous_linear_map.ext_iff.1 (h') x -- for some reason `h'` cannot be substituted here
end

/-- By definition, if `f` is not invertible then `inverse f = 0`. -/
@[simp] lemma inverse_non_equiv (f : M ‚ÜíL[R] M‚ÇÇ) (h : ¬¨‚àÉ (e' : M ‚âÉL[R] M‚ÇÇ), ‚Üëe' = f) :
  inverse f = 0 :=
dif_neg h

end

section
variables [ring R]
variables [add_comm_group M] [topological_add_group M] [module R M]
variables [add_comm_group M‚ÇÇ] [module R M‚ÇÇ]

@[simp] lemma ring_inverse_equiv (e : M ‚âÉL[R] M) :
  ring.inverse ‚Üëe = inverse (e : M ‚ÜíL[R] M) :=
begin
  suffices :
    ring.inverse ((((continuous_linear_equiv.units_equiv _ _).symm e) : M ‚ÜíL[R] M)) = inverse ‚Üëe,
  { convert this },
  simp,
  refl,
end

/-- The function `continuous_linear_equiv.inverse` can be written in terms of `ring.inverse` for the
ring of self-maps of the domain. -/
lemma to_ring_inverse (e : M ‚âÉL[R] M‚ÇÇ) (f : M ‚ÜíL[R] M‚ÇÇ) :
  inverse f = (ring.inverse ((e.symm : (M‚ÇÇ ‚ÜíL[R] M)).comp f)).comp e.symm :=
begin
  by_cases h‚ÇÅ : ‚àÉ (e' : M ‚âÉL[R] M‚ÇÇ), ‚Üëe' = f,
  { obtain ‚ü®e', he'‚ü© := h‚ÇÅ,
    rw ‚Üê he',
    ext,
    simp },
  { suffices : ¬¨is_unit ((e.symm : M‚ÇÇ ‚ÜíL[R] M).comp f),
    { simp [this, h‚ÇÅ] },
    revert h‚ÇÅ,
    contrapose!,
    rintros ‚ü®F, hF‚ü©,
    use (continuous_linear_equiv.units_equiv _ _ F).trans e,
    ext,
    simp [hF] }
end

lemma ring_inverse_eq_map_inverse : ring.inverse = @inverse R M M _ _ _ _ _ _ _ :=
begin
  ext,
  simp [to_ring_inverse (continuous_linear_equiv.refl R M)],
end

end

end continuous_linear_map

namespace submodule

variables
{R : Type*} [ring R]
{M : Type*} [topological_space M] [add_comm_group M] [module R M]
{M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_group M‚ÇÇ] [module R M‚ÇÇ]

open continuous_linear_map

/-- A submodule `p` is called *complemented* if there exists a continuous projection `M ‚Üí‚Çó[R] p`. -/
def closed_complemented (p : submodule R M) : Prop := ‚àÉ f : M ‚ÜíL[R] p, ‚àÄ x : p, f x = x

lemma closed_complemented.has_closed_complement {p : submodule R M} [t1_space p]
  (h : closed_complemented p) :
  ‚àÉ (q : submodule R M) (hq : is_closed (q : set M)), is_compl p q :=
exists.elim h $ Œª f hf, ‚ü®f.ker, f.is_closed_ker, linear_map.is_compl_of_proj hf‚ü©

protected lemma closed_complemented.is_closed [topological_add_group M] [t1_space M]
  {p : submodule R M} (h : closed_complemented p) :
  is_closed (p : set M) :=
begin
  rcases h with ‚ü®f, hf‚ü©,
  have : ker (id R M - (subtype_val p).comp f) = p := linear_map.ker_id_sub_eq_of_proj hf,
  exact this ‚ñ∏ (is_closed_ker _)
end

@[simp] lemma closed_complemented_bot : closed_complemented (‚ä• : submodule R M) :=
‚ü®0, Œª x, by simp only [zero_apply, eq_zero_of_bot_submodule x]‚ü©

@[simp] lemma closed_complemented_top : closed_complemented (‚ä§ : submodule R M) :=
‚ü®(id R M).cod_restrict ‚ä§ (Œª x, trivial), Œª x, subtype.ext_iff_val.2 $ by simp‚ü©

end submodule

lemma continuous_linear_map.closed_complemented_ker_of_right_inverse {R : Type*} [ring R]
  {M : Type*} [topological_space M] [add_comm_group M]
  {M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_group M‚ÇÇ] [module R M] [module R M‚ÇÇ]
  [topological_add_group M] (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
  (h : function.right_inverse f‚ÇÇ f‚ÇÅ) :
  f‚ÇÅ.ker.closed_complemented :=
‚ü®f‚ÇÅ.proj_ker_of_right_inverse f‚ÇÇ h, f‚ÇÅ.proj_ker_of_right_inverse_apply_idem f‚ÇÇ h‚ü©
